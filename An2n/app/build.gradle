apply plugin: 'com.android.application'


def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getEnvOrConsole = { env, prompt ->
    def val = null
    try {
        val = System.getenv(env)
        if (val == null || val == "") {
            if (System.console() != null) {
                val = System.console().readLine("\n" + prompt + ": ")
            }
        }
    } catch (ignored) {
        return null
    }
    return val
}

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "wang.switchy.an2n"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled true //突破方法数65535的限制
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "normal"]
        resConfigs "en", "zh-rCN", "zh-rTW" // 多语言

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    //添加我们的签名文件配置 注意：signingConfigs要写在buildTypes
    signingConfigs {
        debug {}

        release {
            //为我们的release包添加签名文件配置
            storeFile file("An2n.jks")
            storePassword getEnvOrConsole("AN2N_KSTOREPWD", "Keystore password")
            keyAlias "An2n"
            keyPassword getEnvOrConsole("AN2N_KEYPWD", "Key password")
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "API_DEBUG", "true")
            minifyEnabled false //是否启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        release {
            buildConfigField("boolean", "API_DEBUG", "false")
            minifyEnabled false //是否启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                variant.mergedFlavor.versionName = getVersionName()
                def channel = variant.mergedFlavor.manifestPlaceholders.UMENG_CHANNEL_VALUE
                if (variant.buildType.name == "debug") {
                    channel = "test"
                }
                fileName = "An2n_${variant.mergedFlavor.versionName}_${channel}_${variant.buildType.name}.apk"
                outputFileName = fileName
                //output.outputFile = new File(output.outputFile.parent, fileName)
            }
        }
    }

    flavorDimensions "channel"

    productFlavors {
//        xiaomi {
//            dimension "channel"
//            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
//        }
//
//        wandoujia {
//            dimension "channel"
//            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
//        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }


    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:design:25.2.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:25.+'
    implementation 'org.greenrobot:eventbus:3.1.1'

    //集成调试库，能够帮助用户发现埋点错误，上线前务必删除集成调试库，避免无关信息的打印
    implementation 'com.umeng.sdk:common:latest.integration'
    implementation 'com.umeng.sdk:utdid:1.1.5.3'
    implementation 'com.umeng.sdk:analytics:latest.integration'
}
